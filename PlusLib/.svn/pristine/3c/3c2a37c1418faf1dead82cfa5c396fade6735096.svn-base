find_package( Doxygen REQUIRED)

if( DOXYGEN_FOUND )
  if( DOXYGEN_DOT_FOUND )

    # Set up help file format
    option(PLUS_DOCUMENTATION_SEARCH_SERVER_INDEXED "Search index for documentation is generated by th web server. Provides full-text search but only works on web servers." OFF)

    set(GENERATE_HTMLHELP "NO")
    set(SEARCHENGINE "YES")
    set(COMPRESSED_HELP_TARGET_FILE_EXT ".tar.gz")
    if (PLUS_DOCUMENTATION_SEARCH_SERVER_INDEXED)
      set(SERVER_BASED_SEARCH "YES")
    else()
      set(SERVER_BASED_SEARCH "NO")
    endif()
    if(WIN32)
      set(COMPRESSED_HELP_TARGET_FILE_EXT ".chm")
      if (PLUS_DOCUMENTATION_SEARCH_SERVER_INDEXED)
        # search indexing is turned off if HTMLHELP generation is enabled, so we have to disable it
        set(GENERATE_HTMLHELP "NO")
        set(SEARCHENGINE "YES")
      else()
        set(GENERATE_HTMLHELP "YES")
        set(SEARCHENGINE "NO")
      endif()
    endif(WIN32)

    # Documentation for developers, including private members
    set( PlusLib_USER_DOCUMENTATION "NO" )
    set( PlusLib_DEV_DOCUMENTATION "YES" )
    set( PlusLib_ADDITIONAL_EXCLUDE_PATTERN "" )
    set( PlusLib_ADDITIONAL_FILE_PATTERN "*.c \  *.cc \  *.cxx \  *.cpp \  *.c++ \  *.hxx \  *.hpp \  *.h++ \  *.txx \  *.C \  *.CC \  *.C++ \  *.moc" )

    # Parameters for generate_doc.cmake
    set(TARGET_NAME "ApiReference")
    set(UNCOMPRESSED_HELP_TARGET_DIR ${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME})
    set(DOXYGEN_CONFIG_FILE ${UNCOMPRESSED_HELP_TARGET_DIR}/Doxyfile.txt)      
    set(CREATE_COMPRESSED_HELP "YES")
    set(COMPRESSED_HELP_TARGET_DIR "${PLUS_EXECUTABLE_OUTPUT_PATH}/Doc")
    set(COMPRESSED_HELP_TARGET_FILE "PlusLib-${TARGET_NAME}${COMPRESSED_HELP_TARGET_FILE_EXT}")
    # Other parameters
    set(DOC_OUTPUT_FILE "${COMPRESSED_HELP_TARGET_DIR}/${COMPRESSED_HELP_TARGET_FILE}")
    set(DOC_GENERATION_CMAKE_FILE ${UNCOMPRESSED_HELP_TARGET_DIR}/generate_doc.cmake)
    # Generate doxygen configuration and generator file
    configure_file( Doxyfile.txt.in
      ${DOXYGEN_CONFIG_FILE}
      )
    configure_file(
      ${CMAKE_CURRENT_SOURCE_DIR}/generate_doc.cmake.in
      ${DOC_GENERATION_CMAKE_FILE}
      @ONLY
      )
    # Run doxygen as a custom command driven by a custom target.
    add_custom_command(
      OUTPUT ${DOC_OUTPUT_FILE}
      COMMAND ${CMAKE_COMMAND} -P "${DOC_GENERATION_CMAKE_FILE}"
      DEPENDS "${DOC_GENERATION_CMAKE_FILE}"
              "${DOXYGEN_CONFIG_FILE}"
      )
    add_custom_target(Documentation-PlusLib-${TARGET_NAME} DEPENDS ${DOC_OUTPUT_FILE})
    set_target_properties(Documentation-PlusLib-${TARGET_NAME} PROPERTIES LABELS Documentation)
    
  else( DOXYGEN_DOT_FOUND )
    message( STATUS
      "Documentation: Graphviz dot tool not found (http://www.graphviz.org/Download.php, required by Doxygen for diagram generation)- Documentation will not be created"
      )
  endif( DOXYGEN_DOT_FOUND )
endif( DOXYGEN_FOUND )
